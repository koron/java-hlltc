buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    //id 'com.jfrog.bintray'
}

apply plugin: 'com.jfrog.bintray'

group = 'net.kaoriya'
archivesBaseName = 'hlltc'
version = '0.9.1'

dependencies {
    implementation 'com.adgear:metrohash:1.0.0'
    testImplementation 'commons-io:commons-io:2.6'
    testImplementation 'junit:junit:4.12'
}

repositories {
    jcenter()
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'hlltc'
        }
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''

    publications = ['mavenJava']

    pkg {
        repo = 'hlltc'
        name = 'net.kaoriya.hlltc'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/koron/java-hlltc'
        vcsUrl = 'https://github.com/koron/java-hlltc.git'
        issueTrackerUrl = 'https://github.com/koron/java-hlltc/issues'

        version {
            name = project.version
            desc = 'grand beta.2 release'
        }
    }
}
