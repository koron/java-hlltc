// vim:set ts=8 sts=2 sw=2 tw=0:

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'application'
}

repositories {
  mavenCentral()
  google()
}

group = 'net.kaoriya'
archivesBaseName = 'hlltc'
version = '0.11.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation 'com.adgear:metrohash:1.0.0'
    testImplementation 'commons-io:commons-io:2.6'
    testImplementation 'junit:junit:4.13'

    implementation 'com.google.guava:guava:30.0-jre'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainClass.set('net.kaoriya.hlltc_exp1.Example')
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/koron/java-hlltc")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
